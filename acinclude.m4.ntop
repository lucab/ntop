#
# -=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-
# local automake definitions for ntop
## (this file is processed with 'automake' to produce Makefile.in)
# -=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-
#
# Copyright (c) 1998-2003 Luca Deri <deri@ntop.org>
# Updated 1Q 2000 Rocco Carbone <rocco@ntop.org>
# Rewrite 1Q 2003 Burton M. Strauss III <burton@ntopsupport.com>
#
# -=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA 02111-1307, USA.
#

dnl>
dnl>  Check for an ANSI C typedef in a header
dnl>
dnl>  configure.in:
dnl>    AC_CHECK_TYPEDEF(<typedef>, <header>)
dnl>  acconfig.h:
dnl>    #undef HAVE_<typedef>
dnl>

AC_DEFUN(AC_CHECK_TYPEDEF,[dnl
AC_REQUIRE([AC_HEADER_STDC])dnl
AC_MSG_CHECKING(for typedef $1)
AC_CACHE_VAL(ac_cv_typedef_$1,
[AC_EGREP_CPP(dnl
changequote(<<,>>)dnl
<<(^|[^a-zA-Z_0-9])$1[^a-zA-Z_0-9]>>dnl
changequote([,]), [
#include <$2>
], ac_cv_typedef_$1=yes, ac_cv_typedef_$1=no)])dnl
AC_MSG_RESULT($ac_cv_typedef_$1)
if test $ac_cv_typedef_$1 = yes; then
    AC_DEFINE(HAVE_[]translit($1, [a-z], [A-Z]))
fi
])

dnl>
dnl>  Append a value to $LIBS -- allows us to encapsulate functionality
dnl>           for those OSes which DO NOT check subdirectories
dnl>     And to strip dups.
dnl>

# NTOP_APPEND_LIBS(Lvalue, lvalue)
# ----------------------------------------------
AC_DEFUN([NTOP_APPEND_LIBS],  
[dnl
# Expansion of NTOP_APPEND_LIBS($1, $2)
    if test ".$1" != "."; then
        rc=`(echo $LIBS | grep '$1 ' > /dev/null 2> /dev/null; echo $?)`
        if [[ $rc -eq 1 ]]; then 
            case "${DEFINEOS}" in
              DARWIN )
                LIBS="$LIBS -L$1 -L$1/lib"
                ;;
              * )
                LIBS="$LIBS -L$1"
                ;;
            esac
        fi
        if test ".$2" != "."; then
            rc=`(echo $LIBS | grep '\-l$2 ' > /dev/null 2> /dev/null; echo $?)`
            if [[ $rc -eq 1 ]]; then 
                LIBS="$LIBS -l$2"
            fi
        fi
    fi
# Finished expansion of NTOP_APPEND_LIBS()
])

dnl>
dnl>  Append a value to $INCS -- allows us to strip dups
dnl>

# NTOP_APPEND_INCS(Ivalue)
# ----------------------------------------------
AC_DEFUN([NTOP_APPEND_INCS],
[dnl
# Expansion of NTOP_APPEND_INCS($1)
    if test ".$1" != "."; then
        rc=`(echo $INCS | grep '$1' > /dev/null 2> /dev/null; echo $?)`
        if [[ $rc -eq 1 ]]; then
            INCS="$INCS -I$1"
        fi
    fi
# Finished expansion of NTOP_APPEND_INCS()
])

dnl>
dnl>  Report location
dnl>

# NTOP_RPT_LOC(item, lib, include)
# ----------------------------------------------
AC_DEFUN([NTOP_RPT_LOC],
[dnl
# Expansion of NTOP_RPT_LOC($1 $2 $3)
if test ".$2" = "."; then
    echo "$1 library      : standard system libraries"
else
    echo "$1 library      : $2"
fi
if test ".$3" = "."; then
    echo "$1 .h           : standard system headers"
else
    echo "$1 .h           : $3"
fi
# Finished expansion of NTOP_RPT_LOC()
])

# NTOP_SET_LIBINC(item)
# ----------------------------------------------
AC_DEFUN([NTOP_SET_LIBINC],
[dnl
# Expansion of NTOP_SET_LIBINC($1)
if test ".${ac_debug}" = ".yes"; then
    echo "    $1: ${$1_DIRECTORY}, ${$1_LIB}, ${$1_INCLUDE}"
fi
if test ".${$1_DIRECTORY}" != "."; then
    if test ".${$1_LIB}" = "."; then
        if test -d ${$1_DIRECTORY}/lib; then
            $1_LIB="${$1_DIRECTORY}/lib"
        else
            $1_LIB="${$1_DIRECTORY}"
        fi
    fi
    if test ".${$1_INCLUDE}" = "."; then
        if test -d ${$1_DIRECTORY}/include; then
            $1_INCLUDE="${$1_DIRECTORY}/include"
        else
            $1_INCLUDE="${$1_DIRECTORY}"
        fi
    fi
fi
if test ".${ac_debug}" = ".yes"; then
    echo "->  $1: ${$1_DIRECTORY}, ${$1_LIB}, ${$1_INCLUDE}"
fi
# Finished expansion of NTOP_SET_LIBINC()
])
